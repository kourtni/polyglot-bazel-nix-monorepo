load("@aspect_rules_js//js:defs.bzl", "js_library", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config")
load("@npm//:react-scripts/package_json.bzl", cra_bin = "bin")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("cra.bzl", "cra_run_devserver")

npm_link_all_packages()

CRA_DEPS = [
    "//:node_modules/eslint-config-react-app",
    "//projects/react-cra/src",
    "//projects/react-cra/public",
    "//projects/react-cra:package.json",
    "//projects/react-cra:node_modules/react-dom",
    "//projects/react-cra:node_modules/react-scripts",
    "//projects/react-cra:node_modules/react",
    "//projects/react-cra:node_modules/typescript",
    "//projects/react-cra:node_modules/web-vitals",
]

cra_bin.react_scripts(
    # Note: If you want to change the name make sure you update BUILD_PATH below accordingly
    # https://create-react-app.dev/docs/advanced-configuration/
    name = "build",
    args = ["build"],
    srcs = CRA_DEPS,
    env = {"BUILD_PATH": "./build"},
    out_dirs = ["build"],
)

js_test(
    name = "build_smoke_test",
    data = ["build"],
    entry_point = "build_smoke_test.js",
)

cra_run_devserver(
    name = "start",
    args = ["start"],
    command = "node_modules/.bin/react-scripts",
    data = CRA_DEPS,
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = ["//visibility:public"],
)

js_library(
    name = "package_json",
    srcs = ["package.json"],
    visibility = ["//visibility:public"],
)

js_library(
    name = "jest_config",
    srcs = ["jest.config.js"],
    visibility = ["//visibility:public"],
)
