load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:eslint/package_json.bzl", eslint_bin = "bin")

ASSET_PATTERNS = [
    "*.svg",
    "*.css",
]

SRC_PATTERNS = [
    "*.tsx",
    "*.ts",
]

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
TEST_PATTERNS = [
    "*.test.tsx",
    "*.test.ts",
    "*.spec.tsx",
    "*.spec.ts",
]

js_library(
    name = "src",
    srcs = glob(ASSET_PATTERNS),
    visibility = ["//visibility:public"],
    deps = [
        ":src_ts",
    ],
)

ts_project(
    name = "src_ts",
    srcs = glob(
        include = SRC_PATTERNS,
        exclude = TEST_PATTERNS,
    ),
    declaration = True,
    resolve_json_module = True,
    tsconfig = "//projects/react-cra:tsconfig",
    deps = [
        "//projects/react-cra:node_modules/react",
        "//projects/react-cra:node_modules/react-dom",
        "//projects/react-cra:node_modules/web-vitals",
    ],
)

ts_project(
    name = "src_test_ts",
    srcs = glob(TEST_PATTERNS),
    declaration = True,
    resolve_json_module = True,
    supports_workers = False,  # worker bug on macos: ERROR: react-cra/src/BUILD.bazel:50:11: output 'src/App.test.js' was not created
    tsconfig = "//projects/react-cra:tsconfig",
    deps = [
        ":src_ts",
        "//projects/react-cra:node_modules/@testing-library/jest-dom",
        "//projects/react-cra:node_modules/@testing-library/react",
        "//projects/react-cra:node_modules/@types/jest",
    ],
)

jest_test(
    name = "src_test",
    config = "//projects/react-cra:jest_config",
    data = [
        ":src",
        ":src_test_ts",
        "//projects/react-cra:node_modules/jest-environment-jsdom",
        "//projects/react-cra:node_modules/jest-transform-stub",
    ],
    node_modules = "//projects/react-cra:node_modules",
)

eslint_bin.eslint_test(
    name = "eslint_test",
    args = ["{}/{}".format(
        package_name(),
        p,
    ) for p in SRC_PATTERNS],
    data = [
        "//projects/react-cra:package_json",
        "//:node_modules/eslint-config-react-app",
    ] + glob(SRC_PATTERNS),
)
